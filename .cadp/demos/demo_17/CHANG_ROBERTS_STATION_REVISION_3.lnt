module CHANG_ROBERTS_STATION_REVISION_3 (DATA) is

process STATION [OPEN, CLOSE:ACCESS, PRED, SUCC:PORT] (Ai:ADDR, INIT:BOOL) is
   use INIT;
   var B:BOOL in
      B := true;
      loop
         select
            -- timeout: start an election
            SUCC (CLAIM, Ai, B)
         []
            PRED (TOKEN);
            PRIVILEDGE [OPEN, CLOSE, SUCC] (Ai);
            B := not (B)
         []
            var Aj:ADDR, B0:BOOL in
               PRED (CLAIM, ?Aj, ?B0);
               if Ai < Aj then
                  null -- SUCC (CLAIM, Aj, B0) is suppressed for Chang & Roberts
               elsif Ai > Aj then
                  SUCC (CLAIM, Aj, B0)
               elsif B0 == B then
                  PRIVILEDGE [OPEN, CLOSE, SUCC] (Ai);
                  B := not (B)
               end if
            end var
         end select
      end loop
   end var
end process

end module

